#@TYPE: Machine
#@NAME: vexpress-qemu

#@DESCRIPTION: Machine configuration for QEMU running vexpress with system on MTD device

require conf/machine/include/qemu.inc
require conf/machine/include/vexpress.inc

# build only ubifs (used as mender artifact), vexpress-nor (used for qemu and
# testing), mender-image-ubi will append ubimg to built FSTYPES
IMAGE_FSTYPES = "ubifs vexpress-nor"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "kernel-image kernel-devicetree"
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "mtd-utils mtd-utils-ubifs mtd-utils-jffs2"

MKUBIFS_ARGS = "-m 1 -e 262016 -c 1024"
UBINIZE_ARGS = "-p 256KiB -m 1 -O 64"

# NOTE: normally we'd pick up overrides for vexpress-qemu, disable them for now
# to avoid incorrect u-boot patches being applied (on a side note, maybe rework
# u-boot patches to be applied for any machine?)

#MACHINEOVERRIDES =. "vexpress-qemu-flash:vexpress-qemu:"

# Needed to skip particular QA checks that don't apply to U-boot's binary.
INSANE_SKIP_u-boot += "ldflags textrel"

# combined NOR flash is 128MB
MENDER_STORAGE_TOTAL_SIZE_MB = "128"
# reserving 1MB for uboot and additional 1MB for uboot env, pretend it's a
# single boot partition
MENDER_BOOT_PART_SIZE_MB ?= "2"
# 16MB for data partition
MENDER_DATA_PART_SIZE_MB ?= "16"
# align to PEB size 256k
MENDER_PARTITION_ALIGNMENT_KB ?= "256"
# there is no partitioning overhead (no MBR and such)
MENDER_PARTITIONING_OVERHEAD_KB ?= "0"
# Account for UBI overhead, see
# http://www.linux-mtd.infradead.org/doc/ubi.html#L_overhead for details,
# NOTE: the variable is supposed to be used differently, but it is also
# subtracted from MENDER_STORAGE_TOTAL_SIZE_MB, since MTD partitions are fixed
# (and burned into uboot) we need something that will make room fo UBI metadata,
# and setting MENDER_STORAGE_TOTAL_SIZE_MB seems like a good choice.
# The overhead was calculated to be ~1.01MB, round it up to 2MB
MENDER_STORAGE_RESERVED_RAW_SPACE ?= "2097152"
# all of above should give 55296KB (54MB) for rootfs

# since UBIFS employs compression, we override maximum rootfs size and put the
# upper limit at 80MB
IMAGE_ROOTFS_MAXSIZE ?= "81920"

# MTD partitioning has the following layout:
# 1m(u-boot)ro,
# 1m(u-boot-env)ro,
# -(ubi)
#
# NOTE: this affects only u-boot's autoconfigured headers, the same variable is
# also used for generating fw_env.config but because of assumptions made in
# meta-mender-core/recipes-bsp/u-boot/u-boot-fw-utils-mender.inc we need ship
# our own config instead.
MENDER_UBOOT_ENV_STORAGE_DEVICE_OFFSET = "0x100000"

# use ubifs volumes for generating mender artifacts
ARTIFACTIMG_FSTYPE = "ubifs"

# add support for generating NOR Image files
IMAGE_CLASSES += "vexpress-nor_image"
